/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Java Library project to get you started.
 * For more details take a look at the Java Libraries chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.3/userguide/java_library_plugin.html
 */
import org.gradle.plugins.ide.eclipse.model.AccessRule

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

group = "org.antfarmer"
archivesBaseName = "easyjce"
version = '1.0.0'
sourceCompatibility = 1.6
targetCompatibility = 1.6

// add exception to allow use of non-API Sun crypto classes (in tests)
eclipse {
	classpath {
		file {
			whenMerged {
				def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
				jre.accessRules.add(new AccessRule('0', 'sun/security/provider/**'))
			}
		}
	}
}

jar {
	manifest {
		attributes 'Implementation-Title': 'EasyJCE',
				   'Implementation-Version': version
	}
}

repositories {
	// Use jcenter for resolving your dependencies.
	// You can declare any Maven/Ivy/file repository here.
	jcenter()
}

dependencies {
	implementation group: 'org.hibernate', name: 'hibernate-core', version: '4.3.11.Final'
	
	implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.60'
	implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: '5.0.7.RELEASE'
	implementation group: 'de.mkammerer', name: 'argon2-jvm', version: '2.4'
	
	testImplementation group: 'junit', name: 'junit', version: '4.12'
	testImplementation group: 'org.easymock', name: 'easymock', version: '3.6'
	
	testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
	testImplementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
	testImplementation group: 'com.h2database', name: 'h2', version: '1.3.175'
	
	testImplementation files('lib/test/antfarmer-common_app.jar')
}

// copy test resources for testing
task copyTestResources(type: Copy) {
	from ("${projectDir}/src/test/resources") {
		include '**/*.properties'
		include '**/*.xml'
	}
	into "${buildDir}/classes/java/test"
}
processTestResources.dependsOn copyTestResources

javadoc {
	options.with {
		overview "src/main/java/overview.html"
		// links to external docs
		links 'http://docs.jboss.org/hibernate/orm/4.3/javadocs/'
		links 'https://docs.oracle.com/javase/7/docs/api/'
	}
}

// Maven repo requirements ///////////////////////////////////////////////////////////
task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}
tasks.withType(Sign) {
	onlyIf { System.getenv("IS_TRAVIS_CI") != "true" }
}

// Codecov requirements //////////////////////////////////////////////////////////////
jacocoTestReport {
	reports {
		xml.enabled true
		html.enabled false
	}
}

check.dependsOn jacocoTestReport
